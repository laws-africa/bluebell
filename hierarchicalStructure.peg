grammar hierarchicalStructure
  # TODO: coverpage
  # TODO: preface
  # TODO: preamble
  # TODO: conclusions

  root              <- indent body dedent <Root>
  
  body              <- hier_element* <Body>

  # hier elements

  hier_element      <- hier_element_name hier_element_num:hier_element_num? eol
                       indent
                       children:(hier_element / block_element)*
                       dedent <HierElement>

  hier_element_name <- ('CHAPTER' / 'PARA' / 'PART' / 'SUBPART' / 'SECTION' / 'SUBSECTION')

  hier_element_num  <- space num:[^ \n]+

  # block elements

  block_element     <- !dedent content:(table / line) eol <Block>

                     # TODO
  table             <- 'TABLE' <Table>

  line              <- inline+ <Line>
  
  # inline and marker elements

  inline            <- inline_marker / [^\n] <Inline>

                     # TODO: image, others
  inline_marker     <- bold / italics / remark / ref

  # inlines

                     # **foo**
  bold              <- '**' content:(!'**' inline)+ '**' <Bold>

                     # //foo//
  italics           <- '//' content:(!'//' inline)+ '//' <Italics>

                     # [[remark]]
  remark            <- '[[' content:(!']]' inline)+ ']]' <Remark>

                     # [text](href)
  ref               <- '[' content:(!'](' [^\n])* '](' href:[^)\n]* ')' <Ref>

  # terminals

  eol               <- newline empty_line*

  empty_line        <- space? newline

  space             <- ' '+

  newline           <- "\n"

  indent            <- '{' eol

  dedent            <- '}' eol
