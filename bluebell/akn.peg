grammar akn
  # TODO: either one big grammar, or we need to fake inheritance

  root              <- 'noop'

  # ------------------------------------------------------------------------------
  # Judgment
  # ------------------------------------------------------------------------------ 

  judgment          <- indent
                         judgment_body
                         conclusions:conclusions?
                       dedent
                       <Judgment>

  judgment_body     <- introduction?
                       background?
                       arguments?
                       remedies?
                       motivation?
                       decision?

  introduction      <- introduction_marker
                       content:(!background_marker !arguments_marker !remedies_marker !motivation_marker !decision_marker !conclusions_marker hier_block_indent)*
                       <Introduction>

  background        <- background_marker
                       content:(!arguments_marker !remedies_marker !motivation_marker !decision_marker !conclusions_marker hier_block_indent)*
                       <Background>

  arguments         <- arguments_marker
                       content:(!remedies_marker !motivation_marker !decision_marker !conclusions_marker hier_block_indent)*
                       <Arguments>

  remedies          <- remedies_marker
                       content:(!motivation_marker !decision_marker !conclusions_marker hier_block_indent)*
                       <Remedies>

  motivation        <- motivation_marker
                       content:(!decision_marker !conclusions_marker hier_block_indent)*
                       <Motivation>

  decision          <- decision_marker
                       content:(!conclusions_marker hier_block_indent)*
                       <Decision>

  # Judgment markers

  introduction_marker <- 'INTRODUCTION' eol

  background_marker <- 'BACKGROUND' eol

  arguments_marker  <- 'ARGUMENTS' eol

  remedies_marker   <- 'REMEDIES' eol

  motivation_marker <- 'MOTIVATION' eol

  decision_marker   <- 'DECISION' eol

  # ------------------------------------------------------------------------------ 
  # Hierarchical Structure (acts, bills)
  # ------------------------------------------------------------------------------
  # TODO: coverpage
  # TODO: conclusions
  # TODO: attachments

  act               <- hierarchical_structure <Act>

  hierarchical_structure <- indent
                              preface:preface?
                              preamble:preamble?
                              body
                              conclusions:conclusions?
                            dedent
                            <HierarchicalStructure>

  # ------------------------------------------------------------------------------
  # Open structure (doc, statement, debateReport, etc.)
  # ------------------------------------------------------------------------------

  statement         <- openstructure <Statement>

  # TODO: attachments, components
  openstructure     <- indent
                         preface:preface?
                         preamble:preamble?
                         main_body
                         conclusions:conclusions?
                       dedent
                       <OpenStructure>

  # ------------------------------------------------------------------------------
  # Generic hierarchical elements
  # ------------------------------------------------------------------------------

  hier_element      <- crossheading / hier_element_indent

  hier_element_indent <- hier_element_name heading:hier_element_heading? eol
                       indent
                         subheading:subheading?
                         content:(!dedent hier_block_element)*
                       dedent <HierElement>

  hier_element_name <- ('CHAPTER' / 'PARAGRAPH' / 'PART' / 'SUBPART' / 'SECTION' / 'SUBSECTION')

                        # num - heading
                        # - heading
                        # num
                        # TODO: maybe better to make the heading the default?
                        #   eg: heading
                        #       num -
                        #       num - heading
  hier_element_heading <- space num:[^\n-]* heading:('-' space content:inline*)? <HierElementHeading>

  subheading        <- 'SUBHEADING' space content:inline+ eol <Heading>

  hier_block_element <- hier_element / block_element

                     # this is for top-level containers that may/may not start with an
                     # indented block, but otherwise contain block and hier content
  hier_block_indent <- (indent content:hier_block_element+ dedent) / hier_block_element

  # ------------------------------------------------------------------------------
  # Generic containers
  # ------------------------------------------------------------------------------

  preface           <- preface_marker
                       content:(!preamble_marker !body_marker block_indent)*
                       <Preface>

  preamble          <- preamble_marker
                       content:(!body_marker block_indent)*
                       <Preamble>

                     # body that MUST have a hier element
  body              <- body_marker?
                       content:(!conclusions_marker hier_element)*
                       <Body>

                     # body with mixed block and hier elements
  main_body         <- body_marker?
                       content:(!conclusions_marker hier_block_indent)*
                       <MainBody>

  conclusions       <- conclusions_marker
                       content:block_indent*
                       <Conclusions>

  # ------------------------------------------------------------------------------ 
  # Generic blocks
  # ------------------------------------------------------------------------------

                     # this is for top-level containers that may/may not start with an
                     # indented block, but otherwise contain block content
  block_indent      <- (indent content:block_element+ dedent) / block_element

  block             <- indent
                         content:block_element*
                       dedent
                       <Block>

  block_element     <- block / block_elements

                     # these are general block elements that don't care about what block they're in (eg. preface vs body)
  block_elements    <- block_list / table / longtitle / line

  block_list        <- block_item+ <BlockList>

                     # (a)
                     #
                     # (a) preamble
                     #
                     # (a)
                     #   (i) child
                     #
  block_item        <- num:('(' [^)]+ ')') preamble:(eol / (space !block_item block_element)?)
                       content:block?
                       <BlockItem>

                     # TODO
  table             <- 'TABLE' eol <Table>

  longtitle         <- 'LONGTITLE' space content:inline+ eol <Longtitle>

  crossheading      <- 'CROSSHEADING' space content:inline+ eol <Crossheading>

                     # The line block is the only thing that can contain arbitrary text
                     # at the start of a line, where a dedent may occur. By guarding
                     # against it, we don't have to guard at every place that
                     # could start a line.
  line              <- !dedent content:inline+ eol <Line>

  # ------------------------------------------------------------------------------ 
  # Generic inlines and markers
  # ------------------------------------------------------------------------------ 
  
  inline            <- inline_marker / [^\n]

  inline_marker     <- bold / image / italics / remark / ref

                     # **foo**
  bold              <- '**' content:(!'**' inline)+ '**' <Bold>

                     # images like markdown
                     # eg. ![title text](image url)
                     #
                     # the title text is optional, but the enclosing square brackets aren't
  image             <- '![' content:(!'](' [^\n])* '](' href:([^)\n]+) ')' <Image>

                     # //foo//
  italics           <- '//' content:(!'//' inline)+ '//' <Italics>

                     # [[remark]]
  remark            <- '[[' content:(!']]' inline)+ ']]' <Remark>

                     # [text](href)
  ref               <- '[' content:(!'](' [^\n])* '](' href:[^)\n]* ')' <Ref>

  conclusions_marker <- 'CONCLUSIONS' eol

  # ------------------------------------------------------------------------------ 
  # Terminals
  # ------------------------------------------------------------------------------ 

  preface_marker    <- 'PREFACE' eol

  preamble_marker   <- 'PREAMBLE' eol

  body_marker       <- 'BODY' eol

  eol               <- newline empty_line*

  empty_line        <- space? newline

  space             <- ' '+

  newline           <- "\n"

  indent            <- '{' eol

  dedent            <- '}' eol
